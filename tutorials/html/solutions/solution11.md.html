<!doctyle html>
<html>
  <head>
    <meta charset="utf-8">
    <style>
    body {
      counter-reset: section1 section2 section3 section4;
      background-color: white;
    }

    body, p, div, th, td {
      font-family: verdana, helvetica, arial, sans-serif;
      font-size: 10pt;
    }

    h1 {
      text-align: center;
    }

    .author {
      text-align: center;
      font-weight: bold;
    }

    .revision {
      text-align: center;
      font-style: italic;
    }

    h2::before {
      counter-reset: section2 section3 section4;
      counter-increment: section1;
      content: counter(section1) " ";
    }

    h3::before {
      counter-reset: section3 section4;
      counter-increment: section2;
      content: counter(section1) "." counter(section2) " ";
    }

    h4::before {
      counter-reset: section4;
      counter-increment: section3;
      content: counter(section1) "." counter(section2) "." counter(section3) " ";
    }

    table {
      border-collapse: collapse;
      border-spacing: 0px 0px;
      margin: 0 auto;
      page-break-inside: avoid;
    }

    tr {
      page-break-inside: avoid;
      page-break-after: auto;
    }

    th, td {
      padding: 0.2mm 2mm;
    }

    th:not(:last-child),
    td:not(:last-child) {
      border-right: 1px black solid;
    }

    thead > tr {
      border-bottom: 1px black solid;
    }

    tbody > tr:not(:last-of-type) {
      border-bottom: 1px black solid;
    }

    @media print {
      @page {
        padding: 2cm 4cm;
      }

      .beginning-of-doc {
        padding-top: 4cm;
      }

      hr {
        height: 2px;
        background-color: black;
        border: none;
      }


      h2 {
        margin-top: 1.5cm;
        padding-left: 1cm;
        border-bottom: 2px solid black;
        page-break-after: avoid;
      }

      h3 {
        margin-top: 1cm;
        padding-left: 1cm;
        page-break-after: avoid;
      }

      h4 {
        padding-left: 1cm;
        page-break-after: avoid;
      }
    }
    </style>
  </head>
  <body>
    <div class="beginning-of-doc"></div>
    <h1 id="solution11">Solution (11)</h1>

<h3 id="runfullcircle">Run full circle</h3>

<p>To solve the tutorial we should begin with a MainController which has an interface and connections between modules:</p>

<pre><code>package controller11;

import VelocityController;
import PassObjectsController;
import ActiveController;
import SearchParkingPlaceController;
import ParkingController;
import RunTrackController;
import RunTrackVelocityController;
import SteeringAlignController;
import FollowController;
import FollowVelocityController;
import PassCarController;
import OvertakeController;
import OvertakeVelocityController;
import ObstacleController;
import ObstacleVelocityController;
import VelocitySwitchController;
import SteeringSwitchController;

component MainController{
    ports 
        in Q(-200m:200m) frontLeftSensor,
        in Q(-200m:200m) frontRightSensor,
        in Q(-200m:200m) frontLeftSideSensor,
        in Q(-200m:200m) frontRightSideSensor,
        in Q(-200m:200m) backLeftSideSensor,
        in Q(-200m:200m) backRightSideSensor,
        in Q(-200m:200m) backLeftSensor,
        in Q(-200m:200m) backRightSensor,

        in Q time,
        in Q(0km/h:250km/h) velocity,

        in Q(-200m:200m) xPosition,
        in Q(-200m:200m) yPosition,

        out Q(-2m/s^2:2m/s^2) acceleration,
        out Q(-180:180) steering,
        out B status;


    instance VelocityController velocityController;
    connect velocity-&gt;velocityController.velocity;
    connect velocityController.acceleration-&gt;velocitySwitch.accelerationLast;

    instance SearchParkingPlaceController searchParkingPlaceController;
    connect frontLeftSideSensor -&gt; searchParkingPlaceController.frontLeftSideSensor;
    connect backLeftSideSensor -&gt; searchParkingPlaceController.backLeftSideSensor;
    connect searchParkingPlaceController.foundPlace -&gt; velocityController.reverseMove;
    connect searchParkingPlaceController.foundPlace -&gt; parkingController.reverseMove;


    instance ActiveController activeController;
    connect xPosition -&gt; activeController.xPosition;
    connect yPosition -&gt; activeController.yPosition;

    connect activeController.activeSearchParkingPlace -&gt; searchParkingPlaceController.active;
    connect activeController.activeSearchParkingPlace -&gt; velocitySwitch.activeParking;
    connect activeController.activeSearchParkingPlace -&gt; steeringSwitch.activeParking;

    connect activeController.activePassObject -&gt; passObjectsController.active;
    connect activeController.activePassObject -&gt; velocitySwitch.activePassObject;
    connect activeController.activePassObject -&gt; steeringSwitch.activePassObject;

    connect activeController.activeTrack -&gt; velocitySwitch.activeRunTrack;
    connect activeController.activeTrack -&gt; steeringSwitch.activeTrack;

    connect activeController.activeAlign -&gt; velocitySwitch.activeAlign;
    connect activeController.activeAlign -&gt; steeringSwitch.activeAlign;

    connect activeController.followCar -&gt; velocitySwitch.activeFollow;
    connect activeController.followCar -&gt; steeringSwitch.activeFollow;

    connect activeController.overtakeCar -&gt; overtake.active;
    connect activeController.overtakeCar -&gt; steeringSwitch.activeOvertake;

    connect activeController.obstacleWait -&gt; velocitySwitch.activeObstacle;
    connect activeController.obstacleWait -&gt; steeringSwitch.activeObstacle;


    instance ParkingController parkingController;
    connect frontLeftSensor -&gt; parkingController.frontLeftSensor;
    connect frontRightSensor -&gt; parkingController.frontRightSensor;
    connect frontLeftSideSensor -&gt; parkingController.frontLeftSideSensor;
    connect backLeftSideSensor -&gt; parkingController.backLeftSideSensor;
    connect backLeftSensor -&gt; parkingController.backLeftSensor;
    connect backRightSensor -&gt; parkingController.backRightSensor;
    connect parkingController.status -&gt; status;
    connect parkingController.moveForward -&gt; velocityController.moveForward;
    connect parkingController.steeringAngle -&gt; steeringSwitch.steeringParking;

    instance PassObjectsController passObjectsController;
    connect frontLeftSensor -&gt; passObjectsController.frontLeftSensor;
    connect frontRightSensor -&gt; passObjectsController.frontRightSensor;
    connect frontLeftSideSensor -&gt; passObjectsController.frontLeftSideSensor;
    connect frontRightSideSensor -&gt; passObjectsController.frontRightSideSensor;
    connect backLeftSideSensor -&gt;passObjectsController.backLeftSideSensor;
    connect backRightSideSensor -&gt; passObjectsController.backRightSideSensor;
    connect passObjectsController.steering -&gt; steeringSwitch.steeringPassObjects;

    instance RunTrackController runTrackController;
    connect frontLeftSensor -&gt; runTrackController.frontLeft;
    connect frontRightSensor -&gt;  runTrackController.frontRight;
    connect frontLeftSideSensor -&gt; runTrackController.frontLeftSide;
    connect frontRightSideSensor -&gt; runTrackController.frontRightSide;
    connect backLeftSideSensor -&gt; runTrackController.backLeftSide;
    connect backRightSideSensor -&gt; runTrackController.backRightSide;
    connect runTrackController.steering -&gt; steeringSwitch.steeringTrack;

    instance RunTrackVelocityController runTrackVelocity;
    connect velocity -&gt; runTrackVelocity.velocity;
    connect runTrackVelocity.acceleration -&gt; velocitySwitch.accelerationRunTrack;

    instance SteeringAlignController steeringAlign;
    connect frontLeftSideSensor -&gt; steeringAlign.frontLeftSide;
    connect frontRightSideSensor -&gt; steeringAlign.frontRightSide;
    connect backLeftSideSensor -&gt; steeringAlign.backLeftSide;
    connect backRightSideSensor -&gt; steeringAlign.backRightSide;
    connect steeringAlign.steering -&gt; steeringSwitch.steeringAlign;

    instance FollowController&lt;15m&gt; follow;
    connect frontLeftSensor -&gt; follow.frontLeftSensor;
    connect frontRightSensor -&gt; follow.frontRightSensor;
    connect follow.follow -&gt; followVelocity.follow;
    connect follow.run -&gt; followVelocity.run;

    instance FollowVelocityController&lt;2m/s&gt; followVelocity;
    connect velocity -&gt; followVelocity.velocity;
    connect followVelocity.acceleration -&gt; velocitySwitch.accFollow;

    instance PassCarController passCar;
    connect frontRightSideSensor -&gt; passCar.frontRightSideSensor;
    connect backRightSideSensor -&gt; passCar.backRightSideSensor;
    connect passCar.done -&gt; overtake.passed;

    instance OvertakeController&lt;50m&gt; overtake;
    connect frontLeftSensor -&gt; overtake.frontLeftSensor;
    connect frontRightSensor -&gt; overtake.frontRightSensor;
    connect frontRightSideSensor -&gt; overtake.frontRightSideSensor;
    connect frontLeftSideSensor -&gt; overtake.frontLeftSideSensor;
    connect backLeftSideSensor -&gt; overtake.backLeftSideSensor;
    connect backRightSideSensor -&gt; overtake.backRightSideSensor;
    connect overtake.steering -&gt; steeringSwitch.steeringOvertake;
    connect overtake.activatePassChecker -&gt; passCar.active;
    connect overtake.activatePassChecker -&gt; velocitySwitch.activeOvertake;

    instance OvertakeVelocityController overtakeVelocity;
    connect velocity -&gt; overtakeVelocity.velocity;
    connect overtakeVelocity.acceleration -&gt; velocitySwitch.accOvertake;

    instance ObstacleController&lt;30m&gt; obstacle;
    connect frontLeftSensor -&gt; obstacle.frontLeftSensor;
    connect frontRightSensor -&gt; obstacle.frontRightSensor;
    connect obstacle.brake -&gt; obstacleVelocity.brake;

    instance ObstacleVelocityController&lt;5m/s&gt; obstacleVelocity;
    connect velocity -&gt; obstacleVelocity.velocity;
    connect obstacleVelocity.acceleration -&gt; velocitySwitch.accObstacle;

    instance SteeringSwitchController steeringSwitch;
    connect steeringSwitch.steering -&gt; steering;

    instance VelocitySwitchController velocitySwitch;
    connect velocitySwitch.acceleration -&gt; acceleration; 
}
</code></pre>

<p>It has lots of connection and components. But in general we reuse all the controllers from the previous tutorials, due to this fact, the hardest part will be to adjust controllers to combine them together. Let us start with the most important and almost written form the scratch controller, which defines zones where corresponding controller will be activated:</p>

<pre><code>package controller11;

component ActiveController {
    port                                    
        in Q xPosition,
        in Q yPosition,
        out B activePassObject,
        out B activeSearchParkingPlace,
        out B activeTrack,
        out B followCar,
        out B overtakeCar,
        out B obstacleWait,
        out B activeAlign;

    implementation Math{

        activeTrack = 0;
        activePassObject = 0;
        activeSearchParkingPlace = 0;
        activeAlign = 0;
        followCar = 0;
        overtakeCar = 0;
        obstacleWait = 0;

        // default controller
        activeTrack = 1;

        if ((xPosition &lt; 15) &amp;&amp; (xPosition &gt; -130) &amp;&amp; (yPosition &gt; -8) &amp;&amp; (yPosition &lt; 8))
            activePassObject = 1;
            activeTrack = 0;
        end

        if ((xPosition &gt;= 15) &amp;&amp; (xPosition &lt; 50) &amp;&amp; (yPosition &gt; -8) &amp;&amp; (yPosition &lt; 8))
           activeSearchParkingPlace = 1;
           activeTrack = 0;
        end

        if ((xPosition &gt;= 45 ) &amp;&amp; (xPosition &lt; 60) &amp;&amp; (yPosition &gt; 90) &amp;&amp; (yPosition &lt; 110))
           activeAlign = 1;
           activeTrack = 0;
        end

        if ((xPosition &lt; 45 ) &amp;&amp; (xPosition &gt; 20 ) &amp;&amp; (yPosition &gt; 90) &amp;&amp; (yPosition &lt; 110))
           followCar = 1;
           activeTrack = 0;
        end

        if ((xPosition &lt; 20) &amp;&amp; (xPosition &gt; -100) &amp;&amp; (yPosition &gt; 90) &amp;&amp; (yPosition &lt; 110))
           overtakeCar = 1;
           activeTrack = 0;
        end

        if ((xPosition &lt; -90) &amp;&amp; (xPosition &gt; -130) &amp;&amp; (yPosition &gt; 90) &amp;&amp; (yPosition &lt; 110))
           obstacleWait = 1;
           activeTrack = 0;
        end
    }
}
</code></pre>

<p>The activeTrack controller is active by default. The car can drive through the whole track using this controller, but we use it here to pass the curved areas of the track. Then you can see other activators for controllers. The activators fire when the car is located in given area. Then continue with the controller which is default one and was implemented in previous tutorials:</p>

<pre><code>package controller11;

component RunTrackController {
    port
        in Q(-200m:200m) frontLeft,
        in Q(-200m:200m) frontRight,
        in Q(-200m:200m) frontLeftSide,
        in Q(-200m:200m) frontRightSide,
        in Q(-200m:200m) backLeftSide,
        in Q(-200m:200m) backRightSide,
        out Q(-180:180) steering;

    implementation Math{

        Q(0m:1m) threshold = 0.1m;

        B comp1 = (((frontLeft-frontRight) &gt; threshold) &amp;&amp; ((frontLeftSide-backLeftSide) &gt; threshold)) &amp;&amp; ((backRightSide-frontRightSide) &gt; threshold);
        B comp2 = (((frontRight-frontLeft) &gt; threshold) &amp;&amp; ((backLeftSide-frontLeftSide) &gt; threshold)) &amp;&amp; ((frontRightSide-backRightSide) &gt; threshold);

        steering = 0;

        if (comp2)
            steering = 3;
        end

        if (comp1)
            steering = -3;
        end
    }
}
</code></pre>

<p>This controller is responsible for the steering only and we have dedicate one for the velocity control:</p>

<pre><code>package controller11;

component RunTrackVelocityController {
    port
        in Q(0m/s : 10m/s) velocity,
        out Q(-2m/s^2:2m/s^2) acceleration;

    implementation Math{

        if (velocity &gt; 5 m/s)
            acceleration = 0 m/s^2;
        else
            acceleration = 0.5 m/s^2;
        end
    }
}
</code></pre>

<p>After running the first curve part of the track we have to continue with the following a car which is waiting to start movement. But firstly we have to adjust the car to the task, because the goal of the previous controller to have a best trajectory and here we have to align car to the track and the car in front:</p>

<pre><code>package controller11;

component SteeringAlignController {
    port
        in Q(-200m:200m) frontLeftSide,
        in Q(-200m:200m) frontRightSide,
        in Q(-200m:200m) backLeftSide,
        in Q(-200m:200m) backRightSide,
        out Q(-180:180) steering;

    implementation Math{

        Q(0m:1m) thresholdM = 0.5m;
        Q(0m:1m) thresholdS = 0.1m;

        B comp1 = ((frontLeftSide - backLeftSide) &gt; thresholdM);
        B comp2 = ((frontLeftSide - backLeftSide) &lt; -thresholdM);
        B comp3 = ((frontLeftSide - backLeftSide) &gt; thresholdS);
        B comp4 = ((frontLeftSide - backLeftSide) &lt; -thresholdS);

        steering = 0;

        if comp3
            steering = -1;
        end

        if comp4
            steering = 1;
        end

        if comp1
            steering = -3;
        end

        if comp2
            steering = 3;
        end 
    }
}
</code></pre>

<p>This controllers adjust the car to the track. It influences only in the steering. Later you will see another part of the controller which does braking before starting the car following procedure.</p>
  </body>
</html>

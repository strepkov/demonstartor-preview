<!doctyle html>
<html>
  <head>
    <meta charset="utf-8">
    <style>
    body {
      counter-reset: section1 section2 section3 section4;
      background-color: white;
    }

    body, p, div, th, td {
      font-family: verdana, helvetica, arial, sans-serif;
      font-size: 10pt;
    }

    h1 {
      text-align: center;
    }

    .author {
      text-align: center;
      font-weight: bold;
    }

    .revision {
      text-align: center;
      font-style: italic;
    }

    h2::before {
      counter-reset: section2 section3 section4;
      counter-increment: section1;
      content: counter(section1) " ";
    }

    h3::before {
      counter-reset: section3 section4;
      counter-increment: section2;
      content: counter(section1) "." counter(section2) " ";
    }

    h4::before {
      counter-reset: section4;
      counter-increment: section3;
      content: counter(section1) "." counter(section2) "." counter(section3) " ";
    }

    table {
      border-collapse: collapse;
      border-spacing: 0px 0px;
      margin: 0 auto;
      page-break-inside: avoid;
    }

    tr {
      page-break-inside: avoid;
      page-break-after: auto;
    }

    th, td {
      padding: 0.2mm 2mm;
    }

    th:not(:last-child),
    td:not(:last-child) {
      border-right: 1px black solid;
    }

    thead > tr {
      border-bottom: 1px black solid;
    }

    tbody > tr:not(:last-of-type) {
      border-bottom: 1px black solid;
    }

    @media print {
      @page {
        padding: 2cm 4cm;
      }

      .beginning-of-doc {
        padding-top: 4cm;
      }

      hr {
        height: 2px;
        background-color: black;
        border: none;
      }


      h2 {
        margin-top: 1.5cm;
        padding-left: 1cm;
        border-bottom: 2px solid black;
        page-break-after: avoid;
      }

      h3 {
        margin-top: 1cm;
        padding-left: 1cm;
        page-break-after: avoid;
      }

      h4 {
        padding-left: 1cm;
        page-break-after: avoid;
      }
    }
    </style>
  </head>
  <body>
    <div class="beginning-of-doc"></div>
    <h1 id="solution08">Solution(08)</h1>

<h3 id="enhancethepreviouslycreatedparkingcontroller">Enhance the previously created parking controller.</h3>

<p>To solve that tutorial we have to use the MainController, like we did in the other tutorials. We may reuse the previously created modules from the tutorial03: </p>

<pre><code>package controller08;

import VelocityController;
import SearchParkingPlaceController;
import ParkingController;
import MeasureGapController;

component MainController{
    ports 
        in Q(-200m:200m) frontLeftSensor,
        in Q(-200m:200m) frontRightSensor,
        in Q(-200m:200m) frontLeftSideSensor,
        in Q(-200m:200m) frontRightSideSensor,
        in Q(-200m:200m) backLeftSideSensor,
        in Q(-200m:200m) backRightSideSensor,
        in Q(-200m:200m) backLeftSensor,
        in Q(-200m:200m) backRightSensor,

        in Q time,
        in Q(0km/h:250km/h) velocity,

        in Q(-200m:200m) xPosition,
        in Q(-200m:200m) yPosition,

        out Q(-2m/s^2:2m/s^2) acceleration,
        out Q(-180:180) steering,
        out B status;

    instance VelocityController velocityController;
    connect velocity-&gt;velocityController.velocity;
    connect velocityController.acceleration-&gt;acceleration;

    instance MeasureGapController&lt;12m&gt; measureGapController;
    connect velocity -&gt; measureGapController.velocity;
    connect time -&gt; measureGapController.time;
    connect measureGapController.passed -&gt; searchParkingPlaceController.enoughSpace;

    instance SearchParkingPlaceController searchParkingPlaceController;
    connect frontLeftSideSensor-&gt;searchParkingPlaceController.frontLeftSideSensor;
    connect backLeftSideSensor-&gt;searchParkingPlaceController.backLeftSideSensor;
    connect searchParkingPlaceController.activeMeasure -&gt; measureGapController.active;
    connect searchParkingPlaceController.foundPlace-&gt;velocityController.reverseMove;
    connect searchParkingPlaceController.foundPlace-&gt;parkingController.reverseMove;

    instance ParkingController parkingController;
    connect frontRightSensor-&gt;parkingController.frontRightSensor;
    connect frontLeftSensor-&gt;parkingController.frontLeftSensor;
    connect backLeftSensor-&gt;parkingController.backLeftSensor;
    connect backRightSensor-&gt;parkingController.backRightSensor;
    connect frontLeftSideSensor-&gt;parkingController.frontLeftSideSensor;
    connect backLeftSideSensor-&gt;parkingController.backLeftSideSensor;
    connect parkingController.steeringAngle-&gt;steering;
    connect parkingController.status-&gt;status;
    connect parkingController.moveForward-&gt;velocityController.moveForward;
}
</code></pre>

<p>Then we may just completely reuse the VelocityController from the tutorial03:</p>

<pre><code>package controller08;

component VelocityController {
    port                                    
        in Q(0km/h : 250km/h) velocity,
        in B reverseMove,
        in B moveForward,
        out Q(-2m/s^2:2m/s^2) acceleration; 

    implementation Math{                    

        if (velocity &gt; 1 m/s)
            acceleration = 0m/s^2;
        else
            acceleration = 1m/s^2;
        end

        if reverseMove
            acceleration = -0.5 m/s^2;
        end

        if (velocity &lt; -0.5 m/s)
            acceleration = 0m/s^2;
        end

        if (reverseMove &amp;&amp; moveForward)
            acceleration = 0.5 m/s^2;
        end

        if (reverseMove &amp;&amp; moveForward &amp;&amp; (velocity &gt; 0.5 m/s))
            acceleration = 0m/s^2;
        end

    }
}
</code></pre>

<p>After that we may borrow another module from the tutorial03:</p>

<pre><code>package controller08;

component ParkingController {
    port
        in Q(-200m:200m) frontLeftSensor,
        in Q(-200m:200m) frontRightSensor,
        in Q(-200m:200m) frontLeftSideSensor,
        in Q(-200m:200m) backLeftSideSensor,
        in Q(-200m:200m) backLeftSensor,
        in Q(-200m:200m) backRightSensor,
        in B reverseMove,
        out Q steeringAngle,
        out B moveForward,
        out B status;

    implementation Math{

        // fix the state of moving direction
        static Q forwardState = 0;

        moveForward = 0;
        steeringAngle = 0;

        if reverseMove
            steeringAngle = 1;
        end

        if (reverseMove &amp;&amp; (backLeftSensor &lt; 2))
            steeringAngle = -1;
        end

        // Car has to be parallel to the road edge
        // Go forward when the back closer then 3m
        if (reverseMove &amp;&amp; ((backRightSensor == backLeftSensor) || ((backRightSensor &lt; 3) &amp;&amp; (backLeftSensor &lt; 3))))
            forwardState = 1;
        end

        // Stop when the car closer then 3m to front car
        if (((frontRightSensor &lt; 3) || (frontLeftSensor &lt; 3)) &amp;&amp; forwardState)
            status = 1;
        else
            status = 0;
        end

        // if car moving forward, does not change the car angle
        if forwardState
            steeringAngle = 0;
        end

        // align the car
        if (forwardState &amp;&amp; (frontLeftSideSensor &gt; backLeftSideSensor))
            steeringAngle = -0.5;
        end

        if (forwardState &amp;&amp; (frontLeftSideSensor &lt; backLeftSideSensor))
            steeringAngle = 0.5;
        end

        moveForward = forwardState;
    }
}
</code></pre>

<p>Then we may partially reuse the SearchParkingPlaceController from the tutorial03. But it is not sufficient to use the previous functionality of the controller, we have to improve it adding the possibility to measure the parking lot.</p>

<pre><code>package controller08;

component SearchParkingPlaceController {
    port
        in Q(-200m:200m) frontLeftSideSensor,
        in Q(-200m:200m) backLeftSideSensor,
        in B enoughSpace,
        out B activeMeasure,
        out B foundPlace;

    implementation Math{

        static Q passed0 = 0;
        static Q passed1 = 0;
        static Q passed2 = 0;
        static Q passed3 = 0;

        static Q found = 0;
        static B measuresActive = 0;

        if ((frontLeftSideSensor - backLeftSideSensor) &lt; -3)
            passed0 = 1;
        end

        if (((backLeftSideSensor - frontLeftSideSensor) &lt; -3) &amp;&amp; passed0)
            passed1 = 1;
            measuresActive = 1;     // start measuring process
        end

        if (((frontLeftSideSensor - backLeftSideSensor) &lt; -3) &amp;&amp; passed1)
            passed2 = 1;
            measuresActive = 0;     // stop measuring process
        end

        if (((frontLeftSideSensor - backLeftSideSensor) &gt; 3) &amp;&amp; passed2)
            passed3 = 1;
        end

        if enoughSpace
            found = 1;
        end

        if (found &amp;&amp; passed3)
            foundPlace = 1;
        else
            foundPlace = 0;
        end

        if (passed3 &amp;&amp; (found &lt; 1)) // reset the counters for passing cars
            passed0 = 0;
            passed1 = 0;
            passed2 = 0;
            passed3 = 0;
        end

        activeMeasure = measuresActive;
    }
}
</code></pre>

<p>The first part of the controller hasn't been changed, we calculate the difference in measurements between front side sensor and back side sensor to find the gap between cars. Now we should refine it adding the measurement of the space, where it has enough room for the car or not. For this purposes we will use other controller:</p>

<pre><code>package controller08;

component MeasureGapController&lt;Q length = 10m&gt; {
    port
        in Q(0km/h:250km/h) velocity,
        in Q time,
        in B active,
        out B passed;

    implementation Math{

        static Q dist = 0;
        static Q timeLoc = 0;

        if active
            dist += (time - timeLoc) * velocity;
            if (dist &gt;= length)
                passed = 1;
            else
                passed = 0;
            end
        else
            dist = 0;
        end

        timeLoc = time;
    }
}
</code></pre>

<p>This controller measures the distance on the track, between the points of time when it was activated and deactivated. The SearchParkingPlaceController activates it when the front side sensor passed the a first car and it starts to count. When the sensor reached a second car, the MeasureGapController is deactivated by the SearchParkingPlaceController and it stops count the distance. If the distance is satisfy the conditions, and the car has passed the front part of the second car, the parking process starts. If not, then we reset the counters and searching process continue. <br />
The calculation of a distance in the MeasureGapController is done using time and velocity data. We just take an increment of time and multiply it by current velocity. The sufficient parking lot size is defined in the MainController, during the instantiation of the MeasureGapController.</p>
  </body>
</html>
